import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.git.properties)
    alias(libs.plugins.axion.release)
    alias(libs.plugins.lombok)
    alias(libs.plugins.jacoco)
    alias(libs.plugins.application)
    alias(libs.plugins.java)
}

jacoco {
    toolVersion = "0.8.12"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

scmVersion {
    tag { prefix = 'v' }
    versionIncrementer 'incrementMinor'
}
project.version = scmVersion.version

bootJar {
    archiveFileName = "${rootProject.name}.jar"
}

springBoot {
    buildInfo {
        properties {
            name = rootProject.name
            version = project.version
            additional = [
                    host           : InetAddress.localHost.hostName,
                    hostAddress    : InetAddress.localHost.getHostAddress(),
                    operatingSystem: DefaultNativePlatform.currentOperatingSystem.getInternalOs().toString()
            ]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.spring.boot.starter)
    implementation(libs.netflix.eureka.server)
    implementation(libs.jakarta.servlet.api)
    implementation(libs.micrometer.prometheus)
    implementation(libs.owasp.encoder)

    testImplementation(libs.spring.boot.starter.test)
}

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/generoso/sd/ServiceDiscoveryApplication.class"
            ])
        }))
    }
    reports {
        xml.required = true
    }
}
