plugins {
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.lombok)
    alias(libs.plugins.java)
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(libs.cucumber.spring)
    testImplementation(libs.cucumber.java)

    testImplementation(libs.jackson.databing)
    testImplementation(libs.prometheus.scraper)

    testImplementation(libs.spring.boot.starter.test)
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.register('cucumber') {
    def profile = findProperty("spring.profiles.active") ?: "local"
    dependsOn processTestResources, compileTestJava

    doLast {
        javaexec {
            systemProperties = ['spring.profiles.active': profile]
            main = "io.cucumber.core.cli.Main"
            classpath = project.sourceSets.test.runtimeClasspath
            args = ['--tags', 'not @ignore',
                    '--plugin', 'pretty',
                    '--plugin', 'html:reports/cucumber-report.html',
                    '--glue', 'com.generoso.ft.sd',
                    'classpath:features']
        }
    }
}

bootJar.enabled = false
jar.enabled = false
